PROC main:
local mode%, w%

rem ToDo:
rem define app (cassette tape icon)
rem store wifi, host etc. in data file
rem connect to NodeMCU
rem connect to WiFi
rem connect to pistreamer
rem load some graphics - Technics Logo, cassette etc.

statuswin on :font -$3fff,0
w%=gcreate(0,0,416,160,1) :gborder 0 :gtmode 3

rem open serial port & set 9600,8,N,1 with "rsset"
trap LOPEN "TTY:A"
if err
  if err=-41
    print "No 3Link detected!"
    print " "
  endif
  print "Press any key to exit..."
  giprint "TTY:A Error: "+err$(err)
  get
  return
endif
rsset:(16,0,8,1,4,&0)

rem do some nodemcu stuff
lprint "AT"
pause 20
lprint "AT"
pause 20
lprint "ATDT192.168.10.40"
pause 20

do
  dinit "Technics RS-B665 Remote Control"
  dtext "","(Press Esc for STOP)",2
  rem dtext "","www.zedstarr.com",2
  dbuttons "REW",%R,"PLAY",%P,"FF",%F
  mode%=dialog
  if mode%<1
    mode%=%S
  endif
  giprint "Sending: "+upper$(chr$(mode%))
  rem LOPEN "TTY:A";
  LPRINT upper$(chr$(mode%))
  
until 0
ENDP

PROC rsset:(baud%,parity%,data%,stop%,hand%,term&)
  LOCAL frame%,srchar%(6),dummy%,err%
  frame%=data%-5
  IF stop%=2 :frame%=frame% OR 16 :ENDIF
  IF parity% :frame%=frame% OR 32 :ENDIF
  srchar%(1)=baud% OR (baud%*256)
  srchar%(2)=frame% OR (parity%*256)
  srchar%(3)=(hand% AND 255) OR $1100
  srchar%(4)=$13
  POKEL ADDR(srchar%(5)),term&
  err%=IOW(-1,7,srchar%(1),dummy%)
  IF err% :RAISE err% :ENDIF
ENDP

PROC test:
  PRINT "Testing port settings"
  LOPEN "TTY:A"
  rem LOADM "rsset"
  rsset:(15,0,8,1,0,&0)
  LPRINT "Port OK" :LPRINT
  PRINT "Finished" :GET
  LCLOSE
ENDP

PROC testread:
  LOCAL ret%,pbuf%,buf$(255),end%,len%
  PRINT "Test reading from serial port"
  LOPEN "TTY:A"
  REM receive at 2400 without h/shake
  rsset:(11,0,8,1,0,&04002000) REM Control-Z or CR
  pBuf%=ADDR(buf$)
  DO
    REM read max 255 bytes, after leading count byte
    len%=255
    ret%=IOW(-1,1,#UADD(pbuf%,1),len%)
    POKEB pbuf%,len%   REM len% = length actually read
                       REM including terminator char
    end%=LOC(buf$,CHR$(26)) REM non-zero for Control-Z
    IF ret%<0 and ret%<>-43
      BEEP 3,500
      PRINT
      PRINT "Serial read error: ";ERR$(ret%)
    ENDIF
    IF ret%<>-43       REM if received with terminator
      POKEB pbuf%,len%-1 REM remove terminator
      PRINT buf$         REM echo with CRLF
    ELSE
      PRINT buf$;        REM echo without CRLF
    ENDIF
  UNTIL end%
  PRINT "End of session" :PAUSE -30 :KEY
ENDP
